* Listener
STOP LISTENER('SYSTEM.DEFAULT.LISTENER.TCP') IGNSTATE(YES)
DEFINE LISTENER(LSTNR.3014) TRPTYPE(TCP) PORT(3014) CONTROL(QMGR) REPLACE
START LISTENER(LSTNR.3014) IGNSTATE(YES)


* Dead letter queue, for undeliverable messages
DEFINE QLOCAL(DLQ) LIKE(SYSTEM.DEAD.LETTER.QUEUE) REPLACE
ALTER QMGR DEADQ('DLQ')


* Change Maxdepth 
ALTER QLOCAL(SYSTEM.DEFAULT.LOCAL.QUEUE) MAXDEPTH(15000)


* Connection authentication
DEFINE AUTHINFO('CLT.AUTHINFO') AUTHTYPE(IDPWOS) CHCKCLNT(REQDADM) CHCKLOCL(OPTIONAL) ADOPTCTX(YES) REPLACE
ALTER QMGR CONNAUTH('CLT.AUTHINFO')
REFRESH SECURITY(*) TYPE(CONNAUTH)


* Channels (Application + Admin)
DEFINE CHANNEL('CLIENT.ADMIN.SVRCONN') CHLTYPE(SVRCONN) REPLACE
DEFINE CHANNEL('CLIENT.APP.SVRCONN') CHLTYPE(SVRCONN) MCAUSER('app') REPLACE


* Channel authentication rules
SET CHLAUTH('*') TYPE(ADDRESSMAP) ADDRESS('*') USERSRC(NOACCESS) DESCR('Back-stop rule - Blocks everyone') ACTION(REPLACE)
SET CHLAUTH('CLIENT.APP.SVRCONN') TYPE(ADDRESSMAP) ADDRESS('*') USERSRC(CHANNEL) CHCKCLNT(REQUIRED) DESCR('Allows connection via APP channel') ACTION(REPLACE)
SET CHLAUTH('CLIENT.ADMIN.SVRCONN') TYPE(BLOCKUSER) USERLIST('nobody') DESCR('Allows admins on ADMIN channel') ACTION(REPLACE)
SET CHLAUTH('CLIENT.ADMIN.SVRCONN') TYPE(USERMAP) CLNTUSER('admin') USERSRC(CHANNEL) DESCR('Allows admin user to connect via ADMIN channel') ACTION(REPLACE)
SET CHLAUTH('CLIENT.ADMIN.SVRCONN') TYPE(USERMAP) CLNTUSER('admin') USERSRC(MAP) MCAUSER ('mqm') DESCR ('Allow admin as MQ-admin') ACTION(REPLACE)


* Authority records
SET AUTHREC PRINCIPAL('app') OBJTYPE(QMGR) AUTHADD(CONNECT,INQ)
SET AUTHREC PROFILE('**') PRINCIPAL('app') OBJTYPE(QUEUE) AUTHADD(BROWSE,GET,INQ,PUT)
SET AUTHREC PROFILE('**') PRINCIPAL('app') OBJTYPE(TOPIC) AUTHADD(PUB,SUB)
